plugins {
    id 'com.google.cloud.tools.jib' version '3.4.0'
}

dependencies {
    implementation project(':daepiro-core')
    implementation project(':daepiro-common')
    implementation project(':daepiro-redis')
    implementation project(':daepiro-api')
    implementation project(':daepiro-crawler')
    implementation project(':daepiro-auth')

    // feign client
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
}

// system environment variables
def serverIP = System.getenv("EC2_PUBLIC_IP")
def jmxPort = System.getenv("JMX_PORT")
def dockerUserName = System.getenv("DOCKER_USERNAME")
def dockerImageName = System.getenv("DOCKER_IMAGE")

// jib
jib {
    from {
        image = "openjdk:17"
    }
    to {
        image = String.format("%s/%s", dockerUserName, dockerImageName)
        tags = ["latest"]
    }
    container {
        mainClass = 'com.numberone.backend.BackendApplication'
        creationTime = "USE_CURRENT_TIMESTAMP"
        jvmFlags = [
                "-Duser.timezone=Asia/Seoul",
                "-Xms128m", "-Xmx128m",
                "-Dcom.sun.management.jmxremote=true",
                "-Dcom.sun.management.jmxremote.local.only=false",
                "-Dcom.sun.management.jmxremote.port=" + jmxPort.toString(),
                "-Dcom.sun.management.jmxremote.ssl=false",
                "-Dcom.sun.management.jmxremote.authenticate=false",
                "-Djava.rmi.server.hostname=" + serverIP.toString(),
                "-Dcom.sun.management.jmxremote.rmi.port=" + jmxPort.toString(),
                "-XX:+HeapDumpOnOutOfMemoryError",
                "-XX:HeapDumpPath=/heap-dumps/heapdump.hprof"]
        ports = ['8080']
    }
}

bootJar {
    enabled = true // entry module
}
jar {
    enabled = true
}